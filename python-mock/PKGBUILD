# $Id: PKGBUILD 108456 2014-03-27 14:47:54Z fyan $
# Maintainer: jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgbase=python-mock
pkgname=(python2-mock python3-mock)
pkgver=2.0.0
pkgrel=1
pkgdesc='Mocking and Patching Library for Testing'
url='http://www.voidspace.org.uk/python/mock/'
makedepends=('python2' 'python3')
license=('BSD')
arch=('any')
source=("https://pypi.python.org/packages/source/m/mock/mock-$pkgver.tar.gz")
sha256sums=('b158b6df76edd239b8208d481dc46b6afd45a846b7812ff0ce58971cf5bc8bba')
noextract=(mock-${pkgver}.tar.gz)

prepare() {
  [[ -d ${srcdir}/mock-${pkgver} ]] && rm -rf ${srcdir}/mock-${pkgver}
  tar -xzf ${srcdir}/mock-${pkgver}.tar.gz -C ${srcdir} || true
}

build() {
  cd "$srcdir"
  cp -rf "mock-$pkgver" "mock2-$pkgver"

  cd "$srcdir/mock-$pkgver"
  python3 setup.py build

  cd "$srcdir/mock2-$pkgver"
  python2 setup.py build
}

package_python3-mock() {
depends=('python3')
  cd "$srcdir/mock-$pkgver"
  python3 setup.py install --optimize=1 --root="$pkgdir"
  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python2-mock() {
depends=('python2')
  cd "$srcdir/mock2-$pkgver"
  python2 setup.py install --optimize=1 --root="$pkgdir"
  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
