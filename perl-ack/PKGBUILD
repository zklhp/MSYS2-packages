# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=ack
pkgname=perl-${_realname}
pkgver=2.24
pkgrel=1
pkgdesc="A Perl-based grep replacement, aimed at programmers with large trees of heterogeneous source code"
arch=('any')
url="http://betterthangrep.com/"
license=('GPL' 'PerlArtistic')
depends=('perl-File-Next')
groups=('perl-modules')
options=(!emptydirs)
source=("https://www.cpan.org/authors/id/P/PE/PETDANCE/${_realname}-${pkgver}.tar.gz"
        '0002-ack-2.12-match-makefiles-with-more-generality.patch')
sha256sums=('72934009ad8fe712deeb2c41960f2746d0a25e3a226ad7c54232c00105e54f00'
            '1dc49450126a4e765aaade7e90c5e6716b90a7d32d83abba5b324e49e3d190dc')
prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/0002-ack-2.12-match-makefiles-with-more-generality.patch
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  # some standalone tests are failing
  make test || true
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
}
