# Maintainer: atom2013 <atom.long@hotmail.com>

pkgbase=python-pip
pkgname=('python3-pip' 'python2-pip')
pkgver=19.1
pkgrel=1
pkgdesc="The PyPA recommended tool for installing Python packages"
url="https://pip.pypa.io/"
arch=('any')
license=('MIT')
makedepends=('python3' 'python3-setuptools' 'python2' 'python2-setuptools')
source=(pip-${pkgver}.tar.gz::https://github.com/pypa/pip/archive/${pkgver}.tar.gz)
sha256sums=('2fe83f11114528909cb26d453620500cdb5e9a8d677692414d3ddc80dd0fdd79')

package_python3-pip() {
  depends=('python3' 'python3-setuptools')

  cd "${srcdir}/pip-${pkgver}"
  python3 setup.py build
  python3 setup.py install --prefix=/usr --root="${pkgdir}"

  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-pip() {
  depends=('python2' 'python2-setuptools')
  conflicts=('python-pyinstall')
  replaces=('python-pyinstall')

  cd "${srcdir}/pip-${pkgver}"
  python2 setup.py build
  python2 setup.py install --prefix=/usr --root="${pkgdir}"
  
  mv "${pkgdir}/usr/bin/pip" "${pkgdir}/usr/bin/pip2"
  sed -i "s|#!/usr/bin/env python$|#!/usr/bin/env python2|" \
    ${pkgdir}/usr/lib/python2.7/site-packages/pip/__init__.py
  python2 -m compileall ${pkgdir}/usr/lib/python2.7/site-packages/pip/__init__.py
  
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
