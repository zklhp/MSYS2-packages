# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('libyaml' 'libyaml-devel')
pkgver=0.2.1
pkgrel=1
pkgdesc="YAML 1.1 library"
arch=('i686' 'x86_64')
url="https://pyyaml.org/wiki/LibYAML"
license=('MIT')
source=(https://pyyaml.org/download/libyaml/yaml-$pkgver.tar.gz
    'yaml-0.1.7-fix-undefined.patch'
    'yaml-0.1.7-msys2.patch')
sha256sums=('78281145641a080fb32d6e7a87b9c0664d611dcb4d542e90baf731f51cbb59cd'
            'fba2535decf076c0a6b260db01e163e665c5fc92fe7edaa90246d31810082387'
            '59360ea7313e4a022a774c151aa087974325d6458164f50441ff00437a5f76db')

prepare() {
  cd "$srcdir/yaml-$pkgver"

  patch -p1 -i ${srcdir}/yaml-0.1.7-fix-undefined.patch
  patch -p1 -i ${srcdir}/yaml-0.1.7-msys2.patch

  autoreconf -fi
}

build() {
  cd "$srcdir/yaml-$pkgver"
  ./configure --build=${CHOST} --prefix=/usr
  make
  setup
}

setup() {
  cd "$srcdir/yaml-$pkgver"
  make DESTDIR="$srcdir/dest" install
  install -m644 -D LICENSE $srcdir/dest/usr/share/licenses/$pkgname/LICENSE
}

package_libyaml() {
  groups=('libraries')

  install -dm755 $pkgdir/usr/bin
  install -m755 -t ${pkgdir}/usr/bin $srcdir/dest/usr/bin/*.dll
  cp -rf $srcdir/dest/usr/share ${pkgdir}/usr/
}

package_libyaml-devel() {
  groups=('development')
  depends=("libyaml=${pkgver}")

  install -dm755 $pkgdir/usr
  cp -rf $srcdir/dest/usr/include ${pkgdir}/usr/
  cp -rf $srcdir/dest/usr/lib ${pkgdir}/usr/
}
