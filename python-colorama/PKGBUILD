# ArchLinux:
# Maintainer: Alexander Duscheleit <jinks@archlinux.us>
# MSYS2:
# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_pyname=colorama
pkgbase="python-${_pyname}"
pkgname=(python2-${_pyname} python3-${_pyname})
pkgver=0.4.1
pkgrel=1
pkgdesc="Python API for cross-platform colored terminal text."
arch=('any')
url="https://pypi.python.org/pypi/colorama"
license=('BSD')
makedepends=('python2' 'python3' 'git')
checkdepends=('python3-mock' 'python2-mock')
source=(colorama-${pkgver}.tar.gz::https://github.com/tartley/colorama/archive/${pkgver}.tar.gz)
sha256sums=('6b2e2bb7bd96813fcafe5598886ce4f9f741450677e1ee090a119c600f04de9c')


prepare() {
  for pver in {3,2}; do
    rm -rf python${pver} || true
    cp -r ${_pyname}-${pkgver} python${pver}
  done
}

build() {
  for pver in {3,2}; do
    cd "$srcdir/python${pver}"
    /usr/bin/python${pver} setup.py build
  done
}

check() {
  for pver in {3,2}; do
    msg "Python ${pver} test"
    cd "${srcdir}/python${pver}"
    /usr/bin/python${pver}.exe -m unittest discover -p *_test.py || warning "test failed"
  done
}

package_python2-colorama() {
  depends=('python2')

  cd ${srcdir}/python2
  /usr/bin/python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_python3-colorama() {
  depends=('python3')

  cd ${srcdir}/python3
  /usr/bin/python3 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
