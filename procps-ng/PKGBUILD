pkgname=procps-ng
pkgver=3.3.15
pkgrel=1
pkgdesc='Utilities for monitoring your system and its processes'
arch=('i686' 'x86_64')
url='https://gitlab.com/procps-ng/procps'
license=('GPL' 'LGPL')
groups=('sys-utils')
depends=('msys2-runtime' 'ncurses')
makedepends=('ncurses-devel')
options=('!emptydirs' 'strip')
conflicts=('procps')
provides=('procps')
#replaces=('procps')
source=("https://downloads.sourceforge.net/project/${pkgname}/Production/${pkgname}-${pkgver}.tar.xz"
         procps-ng-3.3.12-msys2.patch)
sha256sums=('10bd744ffcb3de2d591d2f6acf1a54a7ba070fdcc432a855931a5057149f0465'
            'e2d692b45dd2ba9840ec5cd6a25a9106bd0ac3ca36d1b4a120288be6575c6977')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/procps-ng-3.3.12-msys2.patch
  autoreconf -fi
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure \
    --build=${CHOST} \
    --prefix=/usr \
    --bindir=/usr/bin \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --libdir=/usr/lib \
    --without-libiconv-prefix \
    --without-libintl-prefix \
    CFLAGS="${CFLAGS} -I/usr/include/ncursesw" \
    --enable-watch8bit \
    --disable-kill
    # kill is provided by util-linux

  make -j1
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
